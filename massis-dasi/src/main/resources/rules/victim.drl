//created on: Apr 16, 2016
package com.massisframework.massis.dasi.apps.robots.victim

//
import java.util.*;
import java.util.concurrent.*;
import com.massisframework.massis.dasi.lowlevel.LowLevelInfo;
import com.massisframework.massis.dasi.agents.tasks.AgentTask;
import com.massisframework.massis.dasi.agents.tasks.SimpleTasksFactory;
import com.massisframework.massis.dasi.events.messages.MessageReceived;
import com.massisframework.massis.dasi.apps.robots.commontasks.*;
import com.massisframework.massis.dasi.apps.robots.*;
import com.massisframework.massis.dasi.apps.robots.messages.*;
import com.massisframework.massis.dasi.agents.goals.*;
import com.massisframework.massis.model.agents.LowLevelAgent;
import com.massisframework.massis.model.building.SimRoom;
import com.massisframework.massis.model.location.Location;

//ruleflow-group([^"]*["][^"]+["])

//declare any global variables here
rule "Generate Goal Memory"
	
	when
		not GoalMemory(  )
 	then
  		insert(new GoalMemory());
  		System.out.println("HEY");
end



rule "Select target"
	
    when
    	$me : VictimRobot()
    	LowLevelInfo($lla : lowLevelAgent)
        not CurrentTarget()
    then
    	SimRoom rndRoom = $lla.getRandomRoom();
        Location rndLoc = rndRoom.getRandomLoc();
		CurrentTarget currentTarget = new CurrentTarget(rndLoc);
		$me.getLogger().info("SELECTING TARGET");
		insert(currentTarget);
end

rule "Less than 1 meter of distance : remove current target"
	
    when
    	LowLevelInfo( $currentLoc : location  )
        $currentTarget : CurrentTarget(location.distance2D($currentLoc)<100)
    then
    	delete($currentTarget);
end


rule "Move to the target (if has energy)"
	
    when
       $me : VictimRobot($energy : energy > 0)
       LowLevelInfo( $currentLoc : location )
       CurrentTarget(location.distance2D($currentLoc)>=100,$targetLoc : location)
    then
	  AgentTask task = new MoveToTarget();
	  task.execute($me,$targetLoc);
end

rule "Send help me message due to lack of energy"
    when
        $me : VictimRobot(energy <= 0 )
    then
       String uuid=UUID.randomUUID().toString();
	   SimpleTasksFactory.sendMessage($me,new IHaveNoEnergy($me.getLocation(), uuid));
end


rule "Received ACK message"
    when
        $me : VictimRobot(energy <= 0 )
        $mr		: MessageReceived(
						data instanceof IKnowYourState,
						sender instanceof RobotAgent,
						$sender  : sender,
						$location : data.info,
						$uuid : data.uuid
 					) from entry-point MESSAGING
    then
	 $me.getLogger().info("Received ACK from "+$sender+". I'm hopeful.");
	 delete($mr);
end



